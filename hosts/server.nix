{ pkgs, lib, self, ... }:

let
  # TODO: add your public ssh key here to be able to log into the deployed host (as user `me`).
  sshKeys = [
    "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAID7wWcuQm1+P34UvkQiu/fyer+PNyvn+j+uh1MRDyhp5 Generated By Termius"
  ];
  backendPort = "3000";
in
{
  # This sets up networking and filesystems in a way that works with garnix hosting.
  garnix.server.enable = true;

  # Enable Docker
  services.docker.enable = true;

  # This is so we can log in.
  services.openssh.enable = true;

  users.users.me = {
    isNormalUser = true;
    description = "me";
    extraGroups = [ "wheel" "systemd-journal" "docker" ];
    openssh.authorizedKeys.keys = sshKeys;
  };

  # Allow sudo without password
  security.sudo.wheelNeedsPassword = false;

  environment.systemPackages = [
    pkgs.htop
    pkgs.tree
    pkgs.docker-compose
  ];

  # Example backend service
  systemd.services.backend = {
    description = "example go backend";
    wantedBy = [ "multi-user.target" ];
    wants = [ "network-online.target" ];
    serviceConfig = {
      Environment = "PORT=" + backendPort;
      Type = "simple";
      DynamicUser = true;
      ExecStart = lib.getExe self.packages.${pkgs.system}.backend;
    };
  };

  # Configure nginx
  services.nginx = {
    enable = true;
    recommendedProxySettings = true;
    recommendedOptimisation = true;
    recommendedGzipSettings = true;

    virtualHosts."default" = {
      locations."/".root = "${self.packages.${pkgs.system}.frontend-bundle}";
      locations."/api".proxyPass = "http://localhost:${backendPort}/";
    };
  };

  networking.firewall.allowedTCPPorts = [ 80 ];

  nixpkgs.hostPlatform = "x86_64-linux";
}
